Alphabet
	a à â b c d e è é f g h i ì j k l m n o ò p q r s t u ù v w x y z
	%*:0 %-
	%+NN:0 %+NN%_P:0
	%+ADJ:0 %+ADJ%_DIM:0 %+ADJ%_IND:0 %+ADJ%_IES:0 %+ADJ%_NUM:0 %+ADJ%_POS:0
	%+PRON:0 %+PRON%_P:0 %+PRON%_DIM:0 %+PRON%_IND:0 %+PRON%_IES:0 %+PRON%_REL:0 %+PRON%_POS:0 %+PRON%_PER:0
	%+ADV:0 %+INT:0 %+PREP:0 %+PREP%_A:0 %+CONJ%_C:0 %+CONJ%_S:0 %+ART:0
	%+V%_ARI:0  %+V%_IRI:0 %+V%_FIN:0 %+V%_NOFIN:0 %+V%_PP:0
	%+INF:0 %+IND:0 %+SUBJ:0 %+COND:0 %+IMP:0 %+PART:0 %+GER:0
	%+PRES:0 %+IMPERF:0 %+PAST:0 %+FUT:0
	%+1:0 %+2:0 %+3:0
	%+MASC:0 %+FEMM:0
	%+SING:0 %+PLUR:0
	%+DIM:0 %+AUM:0 %+VEZA:0 %+VEZB:0 %+PEG:0 %+SUP:0
	%+GLI:0 %+VEL%_A:0 %+VEL%_Y:0
	%+C%_CE:0 %+C%_CI:0 %+C%_GLI:0 %+C%_LA:0 %+C%_LE:0 %+C%_LI:0 %+C%_LO:0 %+C%_ME:0 %+C%_MI:0 %+C%_NE:0 %+C%_SI:0 %+C%_SE:0 %+C%_TE:0 %+C%_TI:0 %+C%_VE:0 %+C%_VI:0
	#:0;

Sets
	Cons   = b c d f g h j k l m n p q r s t v w x y z ;
	Vowel  = a â à e è é i ì o ò u ù ;

Definitions
	Head = %+NN: | %+NN%_P: | %+V%_ARI: |  %+V%_IRI: | %+V%_FIN: | %+V%_NOFIN: | %+V%_PP: | %+ADJ: | %+ADJ%_DIM: | %+ADJ%_IND: | %+ADJ%_IES: | %+ADJ%_NUM: | %+ADJ%_POS: | %+ADV: | %+PRON: | %+PRON%_P: | %+PRON%_DIM: | %+PRON%_IND: | %+PRON%_IES: | %+PRON%_REL: | %+PRON%_POS: | %+PRON%_PER: | %+INT: | %+PREP: | %+PREP%_A: | %+CONJ%_C: | %+CONJ%_S: | %+ART: ;
	Nonverbi =  %+NN: | %+NN%_P: | %+ADJ: | %+ADJ%_DIM: | %+ADJ%_IND: | %+ADJ%_IES: | %+ADJ%_NUM: | %+ADJ%_POS: | %+ADV: | %+PRON: | %+PRON%_P: | %+PRON%_DIM: | %+PRON%_IND: | %+PRON%_IES: | %+PRON%_REL: | %+PRON%_POS: | %+PRON%_PER: | %+INT: | %+PREP: | %+PREP%_A: | %+CONJ%_C: | %+CONJ%_S: | %+ART: ;
	Verbi = %+V%_ARI: |  %+V%_IRI: | %+V%_FIN: | %+V%_NOFIN: | %+V%_PP: ;
	Coniug = %+V%_ARI: |  %+V%_IRI: ;
	Modo = %+INF: | %+IND: | %+SUBJ: | %+COND: | %+IMP: | %+PART: | %+GER: ;
	Tempo = %+PRES: | %+IMPERF: | %+PAST: | %+FUT: ;
	Pers = %+1: | %+2: | %+3: ;
	Genere = %+MASC: | %+FEMM: ;
	Numero = %+SING: | %+PLUR: ;
	Modif = %+DIM: | %+AUM: | %+VEZA: | %+VEZB: | %+PEG: | %+SUP: | %+C%_CE: | %+C%_CI: | %+C%_GLI: | %+C%_LA: | %+C%_LE: | %+C%_LI: | %+C%_LO: | %+C%_ME: | %+C%_MI: | %+C%_NE: | %+C%_SE: | %+C%_SI: | %+C%_TE: | %+C%_TI: | %+C%_VE: | %+C%_VI: ;

Rules

"Inserimento i per c|g con velar = n"
	0:i <=> [c | g] _ (%+GLI:) (Head) (Modo) (Tempo) (Pers) (Genere) (Numero) (Modif) %*: [a | à | o | ò | u | ù] ;

"Inserimento h per c|g con velar = y"
	%+VEL%_Y:h <=> [c | g] _ (Head) (Modo) (Tempo) (Pers) (Genere) (Numero) (Modif) %*: [e | è | é | i | ì] ;

"Inserimento h per c|g con velar = a - 1"
	%+VEL%_A:h <=> [c | g] _ (Nonverbi) (Genere) (Numero) (Modif) %*: [e | è | é] ;

"Inserimento h per c|g con velar = a - 2"
	%+VEL%_A:h <=> [c | g] _ (Nonverbi) (Genere) (Numero) (Modif) %*: i n ;

"Inserimento i per c|g con velar = a"
	%+VEL%_A:i <=> [c | g] _ (Verbi) (Modo) (Tempo) (Pers) (Genere) (Numero) %*: u ;

"Inserimento i per gl con velar = n"
	0:i <=> g l _ (%+GLI:) (Head) (Modo) (Tempo) (Pers) (Genere) (Numero) (Modif) %*: [a | à | e | è | é | o | ò | u | ù] ;

"Eliminazione i con glide = n"
	i:0 <=> _ (%+VEL%_A: | %+VEL%_Y:) (Head) (Modo) (Tempo) (Pers) (Genere) (Numero) (Modif) %*: i [a | à] ;

"Eliminazione i con glide = y"
	i:0 <=> _ %+GLI: (Head) (Modo) (Tempo) (Pers) (Genere) (Numero) (Modif) %*: [i | ì] ;

"Eliminazione i per gn"
	i:0 <=> g n (%+VEL%_A: | %+VEL%_Y: | %+GLI:) (Head) (Modo) (Tempo) (Pers) (Genere) (Numero) (Modif) %*: _ [a | à] ;
